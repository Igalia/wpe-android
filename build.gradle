plugins {
    alias libs.plugins.android.application apply false
    alias libs.plugins.android.library apply false
    alias libs.plugins.kotlin.android apply false
}

tasks.register("clean", Delete) {
    def buildDir = rootProject.layout.buildDirectory.get().asFile
    if (buildDir.exists()) {
        buildDir.eachFile { file ->
            if (file.name != "cerbero" && file.name != "sysroot") {
                delete file
            }
        }
    }
    delete file(".git/hooks/pre-commit")
}

def installQATools = tasks.register("installQATools", Exec) {
    def cacheFile = rootProject.layout.buildDirectory.file(".qaToolsInstalled").get().asFile
    outputs.file(cacheFile)
    commandLine "python3", "-m", "pip", "install", "--upgrade", "editorconfig-checker", "pycodestyle", "cmakelang"
    doLast {
        cacheFile.createNewFile()
    }
}

tasks.register("checkFormat", Exec) {
    dependsOn installQATools
    group = "Verification"
    description = "Checks code format and names"
    commandLine "python3", file("tools/scripts/check-format.py")
}

def installGitHook = tasks.register("installGitHook") {
    dependsOn installQATools
    def hookFile = file(".git/hooks/pre-commit")
    outputs.file(hookFile)
    doLast {
        def scriptFile = file("tools/scripts/check-format.py")
        hookFile.text = "#!/bin/sh\npython3 ${scriptFile} --hook\n"
        hookFile.setExecutable(true)
    }
}

gradle.afterProject { project ->
    project.tasks.all { task ->
        def lowerName = task.name.toLowerCase()
        if ((task.group != "help") &&
            (lowerName.indexOf("clean") < 0) &&
            (lowerName != "wrapper") &&
            (lowerName != "installqatools") &&
            (lowerName != "installgithook")) {
            task.dependsOn(installGitHook)
        }
    }
}
