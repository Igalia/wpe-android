plugins {
    alias libs.plugins.android.application
    alias libs.plugins.kotlin.android
    alias libs.plugins.androidx.navigation.safeargs
}

android {
    namespace = "org.wpewebkit.tools.minibrowser"
    compileSdk = libs.versions.target.android.sdk.get().toInteger()
    buildToolsVersion = libs.versions.android.sdk.build.tools.get()

    defaultConfig {
        minSdk = libs.versions.min.android.sdk.get().toInteger()
        targetSdk = libs.versions.target.android.sdk.get().toInteger()
        versionName = libs.versions.app.version.name.get()
        versionCode = libs.versions.app.version.code.get().toInteger()
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt")
        }
    }

    buildFeatures {
        viewBinding = true
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    packagingOptions {
        jniLibs {
            useLegacyPackaging = true
        }
    }
}

dependencies {
    implementation project(":wpeview")
    implementation libs.android.material
    implementation libs.androidx.appcompat
    implementation libs.androidx.core.ktx
    implementation libs.androidx.fragment.ktx
    implementation libs.androidx.navigation.fragment.ktx
    implementation libs.androidx.navigation.ui.ktx
    implementation libs.androidx.preference.ktx

    modules {
        module("org.jetbrains.kotlin:kotlin-stdlib-jdk7") {
            replacedBy("org.jetbrains.kotlin:kotlin-stdlib", "kotlin-stdlib-jdk7 is now part of kotlin-stdlib")
        }
        module("org.jetbrains.kotlin:kotlin-stdlib-jdk8") {
            replacedBy("org.jetbrains.kotlin:kotlin-stdlib", "kotlin-stdlib-jdk8 is now part of kotlin-stdlib")
        }
    }
}
